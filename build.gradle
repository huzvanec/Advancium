plugins {
    id 'java'
    id 'maven-publish'
    id 'org.jreleaser' version '1.16.0'
}

group = 'cz.jeme.programu'
version = '1.0'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.4-R0.1-SNAPSHOT")
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = group
            artifactId = 'advancium'

            from components.java

            pom {
                name = 'Advancium'
                description = 'A lightweight advancement API for PaperMC servers'
                url = 'https://github.com/huzvanec/Advancium'
                inceptionYear = '2025'

                licenses {
                    license {
                        name = 'GNU General Public License v3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'huzvanec'
                        name = 'Hu≈æva'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/huzvanec/Advancium.git'
                    developerConnection = 'scm:git:ssh://github.com/huzvanec/Advancium.git'
                    url = 'https://github.com/huzvanec/Advancium'
                }
            }
        }
    }

    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}